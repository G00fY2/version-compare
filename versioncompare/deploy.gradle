ext {
    bintrayRepo = 'maven'
    bintrayName = 'version-compare'

    publishedGroupId = 'com.g00fy2'
    publishedArtifactId = 'versioncompare'

    libraryDescription = 'Lightweight library to compare version strings.'

    siteUrl = 'https://github.com/G00fY2/version-compare'
    issueUrl = 'https://github.com/G00fY2/version-compare/issues'
    gitUrl = 'https://github.com/G00fY2/version-compare.git'

    libraryVersion = '1.3.3'

    developerId = 'g00fy2'
    developerName = 'Thomas Wirth'
    developerEmail = 't.wirth@appcom-interactive.de'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ['Apache-2.0']
}

// Maven
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

def pomConfig = {
    licenses {
        license {
            name licenseName
            url licenseUrl
        }
    }
    developers {
        developer {
            id developerId
            name developerName
            email developerEmail
        }
    }
    scm {
        connection gitUrl
        developerConnection gitUrl
        url siteUrl
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId = publishedGroupId
            artifactId = publishedArtifactId
            version = libraryVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('name', publishedArtifactId)
                root.appendNode('description', libraryDescription)
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                configurations.testImplementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'test')
                    }
                }
            }
        }
    }
}

// Bintray
apply plugin: 'com.jfrog.bintray'

group = publishedGroupId
version = libraryVersion

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['mavenJava']
    publish = true
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publicDownloadNumbers = true
        version {
            gpg {
                sign = false
            }
        }
    }
}